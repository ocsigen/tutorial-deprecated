= Client server reactive application with Ocsigen

This is a short tutorial showing how to write a very simple reactive client-server
application using Js_of_ocaml, Eliom and Eliom plus. It requires Eliom > 5.0
(or branch sharedreact of development version while Eliom 5 is not released).

Our application will display a list of items to connected users
(for example a list of messages in a forum),
and make possible to add new items.

In this tutorial, you will learn:
 * How to use Eliom base app to quickly build an application with user management
 * How to create a client-server reactive interface: the HTML is generated indifferently
on server side or on client side and contains reactive parts, that are updated automatically
when data changes
 * How to implement a notification system for your application. Users are notified when a new
item arrives








== First step: a basic application with user management

Eliom base app contains a set of higher level libraries for Eliom
(user management, tips, notifications).
It also contains a template for Eliom-distillery that will create for an application
with user management, that you can use as a starting point for your project.

{{{
eliom-distillery -name tutoreact -template eba.pgocaml
}}}

This template is using Postgresql to save the data.
You need a recent version of Postgresql installed on your system.

Eliom base app is still young and the creation of the database must be improved
(help appreciated). For now you need to create manually a symbolic link to the
program {{{pg_ctl}}} in you directory. For example:

{{{
ln -s /usr/lib/postgresql/9.3/bin/pg_ctl
}}}

Now create the local database and start the database server:

{{{
make db-init
make db-create
make db-update
}}}

Compile and run the program:
{{{
make test.byte
}}}

Now use your browser to go to {{{http://localhost:8080}}}.
Register some user and connect. As the send mail function is not configured,
the activation links will be printed on the console.









== Display messages from db

To make this example more realistic, we suppose we do not want to display all the
messages in the database but only a few of them (for example the list of messages
in a thread in a forum, the blog posts of one user~ .~ .~ .).

In this tutorial we won't implement the database part.
We suppose you have a module {{{Db}}} with these functions:

<<code language="ocaml" class="server"|
val get_messages : unit -> int list Lwt.t
val get_message : int -> string Lwt.t
val add_message : string -> int Lwt.t
>>

Implement them for example using pgocaml, or, for a first version, using Ocsipersist:

<<code language="ocaml" class="server"|
module Db = struct
  let db = Ocsipersist.open_table "messages"
  let last_key =
    Eliom_reference.eref ~persistent:"index" ~scope:Eliom_common.global_scope (-1)
  let get_message id = Ocsipersist.find db (string_of_int id)
  let get_messages () =
    lwt index = Eliom_reference.get last_key in
    let rec aux n l = if n > index then l else aux (n+1) (n::l) in
    Lwt.return (aux 0 [])
  let add_message v =
    lwt index = Eliom_reference.get last_key in
    let index = index + 1 in
    lwt () = Eliom_reference.set last_key index in
    lwt () = Ocsipersist.add db (string_of_int index) v in
    Lwt.return index
end
>>

The content of the main page is defined in file {{{tutoreact.eliom}}}.
Replace the code of {{{main_service_handler}}} by:

<<code language="ocaml" class="server"|
let main_service_handler userid_o () () =
  lwt content = Tutoreact_messages.display userid_o in
  Tutoreact_container.page userid_o content
>>

Create file tutoreact_messages.eliom with the code below.
We will also put module Db here for now.

<<code language="ocaml" class="shared"|
{shared{
  open Eliom_content.Html5
  open Eliom_content.Html5.D
  open Eliom_csreact
}}
>>
<<code language="ocaml" class="server"|
let display userid_o =
  lwt messages = Db.get_messages () in
  lwt l = Lwt_list.map_s (fun id -> lwt msg = Db.get_message id in
                                    Lwt.return (li [pcdata msg]))
      messages
  in
  Lwt.return [ul l]
>>
(Depending on your database, it is probably more efficient to
fetch all messages and their identifiers using only one request).


Compile and run your program:

{{{
make distclean
make test.byte
}}}

To see something, you can add some data manually by calling {{{Db.add_message}}}.





















== Adding new messages

=== Add an input in the page, for connected users

To add an input in the page, replace function {{{display}}} by:

<<code language="ocaml" class="server"|
let display_messages () =
  lwt messages = Db.get_messages () in
  lwt l = Lwt_list.map_s (fun id -> lwt msg = Db.get_message id in
                                    Lwt.return (li [pcdata msg]))
      messages
  in
  Lwt.return (ul l)

let display userid_o =
  lwt messages = display_messages () in
  let l = match userid_o with
    | None -> []
    | _ ->
      let inp = Raw.input ~a:[a_input_type `Text] () in
      [inp]
  in
  Lwt.return (messages::l)
>>

=== Make function {{{Db.add_message}}} accessible from the client

To be able to call a function from the client side program,
use {{{server_function}}}:
<<code language="ocaml" class="server"|
let add_message_rpc =
  server_function
    Json.t<string>
    (Eba_session.connected_rpc (fun userid value -> Db.add_message value))
>>

Parameter {{{Json.t<string>}}} describes the type of function parameter.
It is used for safe unmarshaling on server side of data sent by the client.

We are using the wrapper {{{Eba_session.connected_rpc}}} to make the function
accessible only by connected users. It fails otherwise with an exception.
See also wrapper {{{Eba_session.Opt.connected_rpc}}} if you want to make your
function accessible both from connected and non-connected users.

=== Bind the input to call the function

To call the function from the client program, we will define a //client value//,
that is, a client side expression that is accessible from server side.
The client value will be executed on client side after the page is loaded.
Syntax for client values of type {{{t}}} is {{{ {t{ ... }} }}}.

Replace second branch of the {{{match}}} in function {{{display}}} by:
<<code language="ocaml" class="server"|
let inp = Raw.input ~a:[a_input_type `Text] () in
let _ = {unit{
  let open Lwt_js_events in
  let inp = To_dom.of_input %inp in
  async (fun () -> changes inp (fun _ _ ->
    let value = Js.to_string (inp##value) in
    inp##value <- Js.string "";
    %add_message_rpc value
  ))
}}
in
[inp]
>>

 * We use module {{{Lwt_js_events}}} to manage events.
 * Syntax {{{%v}}} makes possible to use a server side value from client side
 * Function {{{To_dom.of_input}}} returns the JS elements corresponding to the OCaml value {{{%inp}}}
 * {{{Lwt_js_events.async}}} is similair to {{{Lwt.async}}}
 * {{{changes}}} takes a JS element and a function that will be executed
every time a "change" event is received on this element.

This function gets the value of the input, cleans the content of the input,
and calls our server side function. Do not forget the conversions between OCaml strings
and JS strings.

Compile again. Now the messages should be added in the database. But you need to refresh
the page to see them.
















== Structure of a client-server application

We have seen how to send data to the server without stopping the client-side program.
Now we want to update the page.
More generally, the main difference between a Web application and a Web site is that
in the first one, a client side program is running and persists accross HTTP calls (remote procedure calls or page changes). The client process must be able to receive notifications
from the server and update the page accordingly, without regenerating it entirely.
A common practice consists in generating the full interface from client side. But this
is not adapted for all cases. It's usually better to keep the old style Web interaction
and generate pages from server side (for example to make possible indexation by search
engines). In this tutorial, we will see how to generate pages indifferently (and
with the same code) from server or client sides.

In this section, we will introduce two patterns that make possible to write this kind of applications very concisely:
* The client-server cache of data
* Reactive pages

In the following section, we will implement the notification system.

=== Client-server cache

Module {{{Eliom_cscache}}} implements a cache of data, that is, an association table
where you will put the data of your application on client side. For the sake of uniformity,
(pages can be generated either from server or client sides) the cache is also implemented
on server side, with scope request. This avoids to retrieve twice the same data from the
database for the same request.

Create a cache by calling function {{{Eliom_cscache.create}}} from server side.
Implement a function {{{get_data}}} to fetch the data from the database. This function must have an implementation both on server side and client side:

<<code language="ocaml" class="server"|
{server{
  let get_data = Db.get_message

  let get_data_rpc =
    server_function Json.t<int>
      (Eba_session.Opt.connected_rpc (fun userid_o id -> get_data id))
}}
>>
<<code language="ocaml" class="client"|
{client{
   let get_data id = %get_data_rpc id
}}
>>
<<code language="ocaml" class="server"|
{server{
let cache : (int, string) Eliom_cscache.t = Eliom_cscache.create ()
}}
>>
Then call {{{Eliom_cscache.find cache get_data key}}} from either client
or server side to get the value associated to {{{key}}}.
If the value is not present in cache, it will be fetched using function
{{{get_data}}} and added in the cache.

=== Reactive interface

Updating the interface when some data changes is usually not
straightforward. This is usually done by putting identifiers
on elements to find them, and modifying page elements manually
using low level JS functions.

A very elegant solution to simplify this consists in using Functional Reactive Programming (FRP). In reactive programming, you just need to define once
the relation between data and each update produces the recomputation
of all the dependent data. In Ocsigen we are using module
[[http://erratique.ch/logiciel/react|React]] and  [[https://github.com/hhugo/reactiveData/|an extension
for incremental changes in lists]]. Have a look at the documentation
if you are not familiar with FRP.

Module {{{Eliom_content.Html5.R}}} makes possible to define reactive
page elements from client side.

Module {{{Eliom_csreact}}} makes possible to define client side reactive
values from server side, also reactive page elements. To do that, it is
using //shared values//, that is, values that contain both a server
side and a client side value. Basically, use modules {{{SharedReact}}}
and {{{SharedReactiveData}}} instead of {{{React}}}
and {{{ReactiveData}}} to use client-server reactive values.

//Warning: While writing this tutorial,
module Eliom_csreact is still experimental and
all functions are not implemented.//

=== Implementation of the reactive interface

Function {{{display_message}}} must now takes its data from the cache,
and be implemented in a shared section:
<<code language="ocaml" class="shared"|
{shared{
let display_message id =
  lwt msg = Eliom_cscache.find %cache get_data id in
  Lwt.return (li [pcdata msg])
}}
>>

Function {{{display_messages}}} now creates the reactive list of messages identifiers,
and maps page content from this reactive value using module {{{SharedReactiveData}}}.
<<code language="ocaml" class="server"|
let display_messages () =
  lwt messages = Db.get_messages () in
  let rmessages = SharedReactiveData.RList.make messages in
  lwt content = SharedReactiveData.RList.Lwt.map_p
      {shared{ display_message }}
      (fst rmessages)
  in
  Lwt.return (R.ul content)
>>

//In current version (2014-12-08), syntax {{{ {shared{ ...}} }}} is not implemented
for expression. Replace the expression {{{ {shared{ display_message }} }}} by://
<<code language="ocaml" class="server"|
      (Eliom_lib.create_shared_value display_message {{display_message}})
>>




















== Notifications

Now we want to receive notification when a message has been added.
To do that easily, we will use module Eba_notif from Eliom-base-app.

First I define a notification module for the type of data I want clients to be able
to listen on (here lists of message identifiers):
<<code language="ocaml" class="server"|
module Forum_notif = Eba_notif.Make(struct
  type key = unit
  type notification = int
end)
>>

Type {{{key}}} is the type of the identifier of the data we want listen on.
Here there is only one message list for now (thus {{{unit}}}).

Type {{{notification}}} is the type of the notifications to send.
Here: the identifier of the new message to be added in the list.

We define a function to handle notification.
It adds the new identifier in the reactive list of messages:
<<code language="ocaml" class="client"|
{client{

   let handle_notif_message_list rmessages (_, msgid) =
     SharedReactiveData.RList.cons msgid (snd rmessages)

 }}
>>

We notify the server that we are listen on this piece of data by calling (on server side)
function {{{Forum_notif.listen}}}. Notifications are received on client side through
a react event {{{Forum_notif.client_ev ()}}}. We map this event to function
{{{handle_notif_message_list}}}:
<<code language="ocaml" class="server"|
let display_messages () =
  Forum_notif.listen ();
  lwt messages = Db.get_messages () in
  let rmessages = SharedReactiveData.RList.make messages in
  ignore {unit{
    ignore (React.E.map (handle_notif_message_list %rmessages)
            %(Forum_notif.client_ev ()));
  }};
  lwt content = SharedReactiveData.RList.Lwt.map_p
      (Eliom_lib.create_shared_value display_message {{display_message}})
      (fst rmessages)
  in
  Lwt.return (R.ul content)
>>

When we add a message, we notify all the client listening on this piece of data:
<<code language="ocaml" class="server"|
let add_message_rpc =
  server_function
    Json.t<string>
    (Eba_session.connected_rpc
       (fun userid value ->
          lwt id = Db.add_message value in
          Forum_notif.notify () (fun userid -> Lwt.return (Some id));
          Lwt.return ())
       )
>>

The program is now fully functional. You should see the messages being added in the page automatically, even if messages are added by another user. Try with several browser windows.










== More information on cache and client-server reactive data

In this section we will learn a few more things about client-server
programming by implementing some new features in our forum:
* Multi-page forum
* Spinner on client side while loading data

=== Multi-page forum

We now want a forum with several pages, located at URLs
{{{http://localhost:8080/}}}//i//, where //i// is an integer.

==== Services

First we define a new service for this, and register a handler.

<<code language="ocaml" class="server"|
let forum_service =
  Eliom_service.App.service
    ~path:[]
    ~get_params:Eliom_parameter.(suffix (int "i"))
    ()

let forum_service_handler userid_o forumid () =
  lwt content = display userid_o forumid in
  Tutoreact_container.page userid_o content

let () =
  Tutoreact_base.App.register
    forum_service
    (Tutoreact_page.Opt.connected_page forum_service_handler)
>>

We need to add a parameter {{{forumid}}} to function {{{display}}}
(and modify file {{{tutoreact.eliom}}} accordingly, for example to display
forum 0, or something else).

==== Db

Functions Db.get_messages and Db.add_message now take the forum identifier:

<<code language="ocaml" class="server"|
module Db = struct
  let db = Ocsipersist.open_table "messages"
  let dbf = Ocsipersist.open_table "forums"
  let last_key =
    Eliom_reference.eref
      ~persistent:"index" ~scope:Eliom_common.global_scope (-1)
  let get_message id = Ocsipersist.find db (string_of_int id)
  let get_messages forumid =
    try_lwt Ocsipersist.find dbf (string_of_int forumid)
    with Not_found -> Lwt.return []
  let add_message forumid v =
    lwt index = Eliom_reference.get last_key in
    let index = index + 1 in
    lwt () = Eliom_reference.set last_key index in
    lwt () = Ocsipersist.add db (string_of_int index) v in
    lwt l = get_messages forumid in
    lwt () = Ocsipersist.add dbf (string_of_int forumid) (index::l) in
    Lwt.return index
end
>>

Also function {{{add_message_rpc}}} takes the forum id as new parameter:

<<code language="ocaml" class="shared"|
{shared{
  type add_message_type = int * string deriving (Json)
}}
>>
<<code language="ocaml" class="server"|
let add_message_rpc =
  server_function
    Json.t<add_message_type>
    (Eba_session.connected_rpc
       (fun userid (forumid, value) ->
          lwt id = Db.add_message forumid value in
          Forum_notif.notify () (fun userid -> Lwt.return (Some id));
          Lwt.return ())
       )
>>
<<code language="ocaml" class="client"|
          ...
          %add_message_rpc (%forumid, value)
>>

Update function {{{display}}} accordingly, and add parameter {{{forumid}}}
to {{{display_messages}}}.


==== Cache of forum message identifiers

We must send the notifications only to the clients listening on the same forum.

We will create a new client-server cache to keep the reactive list of
message identifiers for each forums:

<<code language="ocaml" class="server"|
let forumcache : (int, int SharedReactiveData.RList.t
                       * int SharedReactiveData.RList.handle) Eliom_cscache.t =
  Eliom_cscache.create ()
>>

Implement the equivalent of {{{get_data}}} for this new cache.

**Be very careful:**
In function {{{get_data_forum}}}, we must find the reactive list
of messages in the new cache — if it exists — instead of creating a new one!
Otherwise you will have several reactive data for the same forum
and the page updates will fail!

To do that, we give as optional argument {{{?default}}} to function
{{{SharedReactiveData.RList.make}}}, a client value containing
(optionnaly) the current reactive list. If it does not exist in
the cache, a new one will be created like before:
<<code language="ocaml" class="server"|
{server{

  let get_data_forum forumid =
    lwt messages = Db.get_messages forumid in
    let default = {'a option{
      try Some (Eliom_cscache.find_if_ready %forumcache %forumid)
      with _ -> None
    }}
    in
    Lwt.return (SharedReactiveData.RList.make ~default messages)

  let get_data_forum_rpc =
    server_function Json.t<int>
      (Eba_session.Opt.connected_rpc
        (fun userid_o forumid -> get_data_forum forumid))

}}
>>
<<code language="ocaml" class="client"|
{client{

   let get_data_forum forumid = %get_data_forum_rpc forumid

}}
>>

Function {{{display_messages}}} now takes the reactive list from the cache:
<<code language="ocaml" class="server"|
let display_messages forumid =
  Forum_notif.listen ();
  lwt rmessages = Eliom_cscache.find forumcache get_data_forum forumid in
  ...
>>





==== Notifications dependent on forum id

Notifications must now depend on the identifier.
We want to receive notifications only for the forums present in client-side
cache of forums. We just chanhe the type {{{key}}} of module {{{Forum_notif}}}
to use an integer (instead of {{{unit}}}):
<<code language="ocaml" class="server"|
module Forum_notif = Eba_notif.Make(struct
  type key = int (* The type of the identifier of the data we want listen on.
                    Here forum identifiers *)
  type notification = int (* Message identifier *)
end)
>>

Functions {{{Forum_notif.notify}}} and {{{Forum_notif.listen}}}
now take the forum id.

<<code language="ocaml" class="server"|
  Forum_notif.notify forumid (fun userid -> Lwt.return (Some id))
  ...
  Forum_notif.listen forumid;
>>

Function {{{handle_notif_message}}} now takes the reactive list {{{rmessage}}}
from cache:
<<code language="ocaml" class="client"|
{client{
   let handle_notif_message_list (forumid, msgid) =
     try
       let rmessages = Eliom_cscache.find_if_ready %forumcache forumid in
       SharedReactiveData.RList.cons msgid (snd rmessages)
     with Not_found | Eliom_cscache.Not_ready -> ()
 }}
>>



=== Display a spinner while loading the messages

Retrieving messages from server can take time.
To display a spinner while loading the message, replace function
{{{display_message}}} by:

<<code language="ocaml" class="shared"|
{shared{
let display_message id =
  let th = (lwt msg = Eliom_cscache.find %cache get_data id in
            Lwt.return (div [pcdata msg]))
  in
  lwt v = Ow_spinner.with_spinner th in
  Lwt.return (li [v])
 }}
>>

To simulate network latency,
you can add a {{{Lwt_unix.sleep}}} in server side's {{{get_data}}} function.




== The full code

<<code language="ocaml" class="server"|
module Db = struct
  let db = Ocsipersist.open_table "messages"
  let dbf = Ocsipersist.open_table "forums"
  let last_key =
    Eliom_reference.eref
      ~persistent:"index" ~scope:Eliom_common.global_scope (-1)
  let get_message id = Ocsipersist.find db (string_of_int id)
  let get_messages forumid =
    try_lwt Ocsipersist.find dbf (string_of_int forumid)
    with Not_found -> Lwt.return []
  let add_message forumid v =
    lwt index = Eliom_reference.get last_key in
    let index = index + 1 in
    lwt () = Eliom_reference.set last_key index in
    lwt () = Ocsipersist.add db (string_of_int index) v in
    lwt l = get_messages forumid in
    lwt () = Ocsipersist.add dbf (string_of_int forumid) (index::l) in
    Lwt.return index
end
>>
<<code language="ocaml" class="shared"|

{shared{
  open Eliom_content.Html5
  open Eliom_content.Html5.D
  open Eliom_csreact
}}
>>
<<code language="ocaml" class="server"|

(* Notifications on message list *)
module Forum_notif = Eba_notif.Make(struct
  type key = int (* The type of the identifier of the data we want listen on.
                    Here forum identifiers *)
  type notification = int (* Message identifier *)
end)

>>
<<code language="ocaml" class="shared"|
{shared{
  type add_message_type = int * string deriving (Json)
}}

>>
<<code language="ocaml" class="server"|
let add_message_rpc =
  server_function
    Json.t<add_message_type>
    (Eba_session.connected_rpc
       (fun userid (forumid, value) ->
          lwt id = Db.add_message forumid value in
          Forum_notif.notify forumid (fun userid -> Lwt.return (Some id));
          Lwt.return ())
       )


let cache : (int, string) Eliom_cscache.t = Eliom_cscache.create ()
let forumcache : (int, int SharedReactiveData.RList.t
                       * int SharedReactiveData.RList.handle) Eliom_cscache.t =
  Eliom_cscache.create ()


let get_data = Db.get_message

let get_data_rpc =
  server_function Json.t<int>
    (Eba_session.Opt.connected_rpc (fun userid_o id ->
       lwt () = Lwt_unix.sleep 1. in get_data id))

>>
<<code language="ocaml" class="client"|
{client{

   let get_data id = %get_data_rpc id

}}
>>
<<code language="ocaml" class="server"|
{server{

  let get_data_forum forumid =
    lwt messages = Db.get_messages forumid in
    let default = {'a option{
      try Some (Eliom_cscache.find_if_ready %forumcache %forumid)
      with _ -> None
    }}
    in
    Lwt.return (SharedReactiveData.RList.make ~default messages)

  let get_data_forum_rpc =
    server_function Json.t<int>
      (Eba_session.Opt.connected_rpc
        (fun userid_o forumid -> get_data_forum forumid))

}}
>>
<<code language="ocaml" class="client"|
{client{

   let get_data_forum forumid = %get_data_forum_rpc forumid

}}
>>
<<code language="ocaml" class="shared"|

{shared{
let display_message id =
  let th = (lwt msg = Eliom_cscache.find %cache get_data id in
            Lwt.return (div [pcdata msg]))
  in
  lwt v = Ow_spinner.with_spinner th in
  Lwt.return (li [v])
 }}

>>
<<code language="ocaml" class="client"|
{client{

   let handle_notif_message_list rmessages (_, msgid) =
     SharedReactiveData.RList.cons msgid (snd rmessages)

 }}

>>
<<code language="ocaml" class="server"|
let display_messages forumid =
  Forum_notif.listen forumid;
  lwt rmessages = Eliom_cscache.find forumcache get_data_forum forumid in
  ignore {unit{
    ignore (React.E.map (handle_notif_message_list %rmessages)
            %(Forum_notif.client_ev ()));
  }};
  lwt content = SharedReactiveData.RList.Lwt.map_p
      (Eliom_lib.create_shared_value display_message {{display_message}})
      (fst rmessages)
  in
  Lwt.return (R.ul content)

let display userid_o forumid =
  lwt messages = display_messages forumid in
  let l = match userid_o with
    | None -> []
    | _ ->
      let inp = Raw.input ~a:[a_input_type `Text] () in
      let _ = {unit{
        let open Lwt_js_events in
        let inp = To_dom.of_input %inp in
        async (fun () -> changes inp (fun _ _ ->
          let value = Js.to_string (inp##value) in
          inp##value <- Js.string "";
          %add_message_rpc (%forumid, value)
        ))
      }}
      in
      [inp]
  in
  Lwt.return (messages::l)



let forum_service =
  Eliom_service.App.service
    ~path:[]
    ~get_params:Eliom_parameter.(suffix (int "i"))
    ()

let forum_service_handler userid_o forumid () =
  lwt content = display userid_o forumid in
  Tutoreact_container.page userid_o content

let () =
  Tutoreact_base.App.register
    forum_service
    (Tutoreact_page.Opt.connected_page forum_service_handler)
>>